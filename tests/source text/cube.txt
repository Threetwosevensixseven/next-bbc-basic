    5 FILLED%=0
   10 REM Initialise the data
   20 READ CN%
   30 DIM X%(CN%),Y%(CN%),Z%(CN%),A%(CN%),B%(CN%)
   40 FOR I%=1 TO CN%: READ X%(I%),Y%(I%),Z%(I%):NEXT
   50 READ TS%
   60 DIM S%(TS%,4)
   70 FOR I%=1 TO TS%: READ S%(I%,1),S%(I%,2),S%(I%,3),S%(I%,4):NEXT
   80 REM Variables
   90 THETA=0:PSI=0:PHI=0
  100 XD=0:YD=0
  110 SD=256:OD=128
  120 REM Transform
  130 FOR I%=1 TO CN%
  140 REM Rotate
  150 XX=X%(I%):YY=Y%(I%):ZZ=Z%(I%)
  160 Y=YY*COS(PHI/180*PI)-ZZ*SIN(PHI/180*PI)
  170 ZZ=YY*SIN(PHI/180*PI)+ZZ*COS(PHI/180*PI)
  180 X=XX*COS(THETA/180*PI)-ZZ*SIN(THETA/180*PI)
  190 ZZ=XX*SIN(THETA/180*PI)+ZZ*COS(THETA/180*PI)
  200 XX=X*COS(PSI/180*PI)-Y*SIN(PSI/180*PI)
  210 YY=X*SIN(PSI/180*PI)+Y*COS(PSI/180*PI)
  220 REM Transform
  230 XX=XX+XD:YY=YY+YD
  240 REM Perspective
  250 A%(I%)=128+XX*SD/(OD-ZZ)
  260 B%(I%)=96+YY*SD/(OD-ZZ)
  270 NEXT
  280 REM Draw
  285 CLS
  290 FOR I%=1 TO TS%
  300 C1%=S%(I%,1):C2%=S%(I%,2):C3%=S%(I%,3):C4%=S%(I%,4)
  310 X1=A%(C1%):X2=A%(C2%):X3=A%(C3%):X4=A%(C4%)
  320 Y1=B%(C1%):Y2=B%(C2%):Y3=B%(C3%):Y4=B%(C4%)
  330 IF X1*(Y2-Y3)+X2*(Y3-Y1)+X3*(Y1-Y2) > 0 THEN GOTO 380
  335 GCOL 0,I%MOD7+1
  336 IF FILLED%=0 THEN GOTO 360
  340 MOVE X1,Y1:MOVE X2,Y2:PLOT 85,X3,Y3
  350 MOVE X3,Y3:MOVE X4,Y4:PLOT 85,X1,Y1
  355 GOTO 380
  360 MOVE X1,Y1:PLOT 5,X2,Y2:PLOT 5,X3,Y3:PLOT 5,X4,Y4:PLOT 5,X1,Y1
  380 NEXT
  390 PHI=PHI+4
  391 THETA=THETA-1
  400 GOTO 130
 1000 REM DATA
 1010 DATA 8
 1020 DATA -20,20,20
 1030 DATA 20,20,20
 1040 DATA -20,-20,20
 1050 DATA 20,-20,20
 1060 DATA -20,20,-20
 1070 DATA 20,20,-20
 1080 DATA -20,-20,-20
 1090 DATA 20,-20,-20
 1100 DATA 6
 1110 DATA 1,2,4,3
 1120 DATA 7,8,6,5
 1130 DATA 2,6,8,4
 1140 DATA 3,7,5,1
 1150 DATA 3,4,8,7
 1160 DATA 1,5,6,2
