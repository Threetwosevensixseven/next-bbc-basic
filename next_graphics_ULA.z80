;
; Title:	BBC Basic Interpreter - Z80 version
;		Spectrum Next ULA Graphics Routines
; Author:	Dean Belfield
; Created:	13/05/2021
; Last Updated:	12/07/2021
;
; Modinfo:
; 15/05/2021:	Plot now pulls PLOT_MODE and PLOT_COLOUR direct from RAM; Updates for MODE command
; 17/05/2021:	Tweaks for COLOUR, GCOL
; 19/05/2021:	Fixed bug in Get_Char
; 20/05/2021:	Added Draw_Horz_Line; Fixed bug in Set_Attr
; 22/05/2021:	Added Point; Minor optimisations
; 26/05/2021:	Added support code for Layer 2 modes 2 and 3
; 28/05/2021:	Removed Display_Cursor
; 04/06/2021:	Point and Triangle now work correctly with scroll offset
; 06/06/2021:	CLS now uses DMA
; 08/06/2021:	Fixed default palette and reset sprite clipping
; 11/06/2021:	Now uses NREG definitions in equs.z80
; 21/06/2021:	Added support for CLG; Tweaks to Get_Char
; 12/07/2021:	Added support for UDGs

			MODULE	NEXT_GRAPHICS_ULA

; Set the video mode up
;
Set_Video_Mode:		LD	A, %01000111
			LD	(TEXT_FOREGROUND), A 
			LD	(PLOT_COLOUR), A
			LD	A, 32
			LD	(CHAR_COLS), A 
			LD	A, 24
			LD	(CHAR_ROWS), A
			LD	BC, 256
			LD	(PIXEL_WIDTH), BC 
			LD	BC, 192 
			LD	(PIXEL_HEIGHT), BC
			CALL	NEXT_GRAPHICS.CLS
			NEXTREG NREG.LAYER_CONTROL, %00101001	; Enable sprites & clipping, SUL priority
			NEXTREG	NREG.L2_CONTROL, %00000000	; Reset Layer 2 resolution
			NEXTREG	NREG.CLIP_WINDOW_L2, 0		
			NEXTREG	NREG.CLIP_WINDOW_L2, 255
			NEXTREG	NREG.CLIP_WINDOW_L2, 0
			NEXTREG	NREG.CLIP_WINDOW_L2, 191	; Resetting Layer 2 clipping
			NEXTREG	NREG.CLIP_WINDOW_SPRITES, 0	; Clip Window Sprites
			NEXTREG	NREG.CLIP_WINDOW_SPRITES, 255
			NEXTREG	NREG.CLIP_WINDOW_SPRITES, 0
			NEXTREG	NREG.CLIP_WINDOW_SPRITES, 191 + 32
			NEXTREG	NREG.PALETTE_CONTROL, %10000000 ; Change ULA first palette
			RET	

; Clear the screen
; Used by CLS and CLG
; C: Colour to clear screen with
;
Clear_Screen:		LD	A, (BORDER_COLOUR)
			OUT	(254), A
			LD	A, C 
			LD	(R00), A
			XOR	A
			LD	DE, 16384
			LD	BC, 6144
			CALL	NEXT_DMA.FillDMA
			LD	A, (R00)
			LD	DE, 22528
			LD	BC, 768
			CALL	NEXT_DMA.FillDMA
			NEXTREG	NREG.ULA_SCROLL_Y, 0		; Reset scroll
			RET

; Scroll
;  H: Line to clear after scroll
;
Set_Scroll_Reg:		LD	A, (SCRLPOS_Y)
			ADD	A, A
			ADD	A, A
			ADD	A, A 
			NEXTREG	NREG.ULA_SCROLL_Y, A 
			RET

; Adjust HL for scroll position
; HL: Y
; Returns:
; HL: Y adjusted for Y 
;
Adjust_SCRLPOS_P:	LD	A, (SCRLPOS_Y)		; Get Y scroll position
			ADD	A, A
			ADD	A, A 
			ADD	A, A 			; Multiply by 8
			ADD	A, L 			; Add to pixel Y position
			JR	C, 0F 			; If we've wrapped, then skip
			CP	192			; Gone off screen?
			JR	C, 1F
0:			SUB	192
1:			LD	L, A
			RET

; Print a single character out to an X/Y position
;  E: Character to print
;  L: X Coordinate
;  H: Y Coordinate
;
Print_Char:		CALL 	Get_Char_Address
			CALL	Get_Character_Data
			LD	B,8			; Loop counter
1:			LD 	A,(DE)			; Get the byte from the character in A
			LD 	(HL),A			; Stick A onto the screen
			INC 	DE			; Goto next byte of character
			INC 	H			; Goto next line on screen
			DJNZ 	1B			; Loop around whilst it is Not Zero (NZ)
			LD	A, H 			; And set the attribute up too
			RRA
			RRA 
			RRA
			DEC	A
			AND 	3 
			OR	88
			LD	H, A 
			LD	A,(TEXT_FOREGROUND)
			LD	(HL), A
			RET

; Scrape character off screen
;  L: X coordinate
;  H: Y coordinate
; Returns:
; R00 to R07: Character data in monochrome bitmap format
;
Get_Char:		CALL	Get_Char_Address	; Get character address in HL
			LD	DE, R00			; Storage for bitmap data
			LD	B, 8 
1:			LD	A, (HL)
			LD	(DE), A 
			INC	H
			INC	E 
			DJNZ	1B 
			RET

; Get screen address
;  H: Y character position
;  L: X character position
; Returns address in HL
;
Get_Char_Address:	LD	A,H
			AND	%00000111
			RRCA
			RRCA 
			RRCA 
			OR	L
			LD	L,A
			LD	A,H
			AND	%00011000
			OR	%01000000
			LD	H,A
			RET				; Returns screen address in HL

; Get the attribute address
;  H: Y character position
;  L: X character position
; Return address in HL
;
Get_Attr_Address:	PUSH	DE
			LD 	D, 32			; Multiply Y by 32
			LD 	E, H
			LD	A, L			; Get the X offset
			MUL 	D, E 			; Multiply Y by 32
			ADD	DE, A			; Add X
			EX	DE, HL
			LD 	A, H
			ADD 	A, 0x58			; Add the attribute 
			LD 	H, A
			POP	DE
			RET

; Read a point
; HL: X
; DE: Y
;	
Point:			CALL	Adjust_SCRLPOS_P
			LD	D, L			; D: Y Coordinate
			PIXELAD				; Get the pixel address in HL
			SETAE				; Get pixel position in A
			AND	(HL)			; Mask out pixel
			RET	Z 
			LD	A, 1
			RET 
			
; Plot a point
; HL: Y
; DE: X
;	
Plot:			LD	A, (PLOT_MODE)		; Check plot mode is valid
			CP	7
			RET	NC			; Do nothing if plot mode >=7
			CALL	Adjust_SCRLPOS_P
1:			LD	D, L 			; D = Y, E = X
			PIXELAD				; Get the pixel address in HL
			SETAE				; Get pixel mask in A
			LD	E, A 			;  E: Pixel mask
			LD	A, (PLOT_MODE)
			CALL	SWITCH_A
			DEFW	Plot_OR
			DEFW	Plot_OR
			DEFW	Plot_AND
			DEFW	Plot_XOR 
			DEFW	Plot_XOR
			DEFW	Plot_NOP
			DEFW 	Plot_AND 

Plot_NOP:		RET				; Jump to plot routine (address stacked previous)

Plot_OR:		LD	A, (HL)
			OR	E			; OR with screen
			LD	(HL), A
			JR	Plot_Attribute

Plot_AND:		LD	A, E			; AND with screen
			CPL 
			AND	(HL)
			LD	(HL), A 
			JR	Plot_Attribute 

Plot_XOR:		LD	A, (HL)
			XOR	E			; XOR with screen
			LD	(HL), A
			JR	Plot_Attribute 

; Write colour value out to ULA attribute map
; HL: Address in ULA pixel map
;			
Plot_Attribute:		LD	A, H 			; Get the attribute address from the screen address
			RRA
			RRA 
			RRA 
			AND 	3 
			OR	88
			LD	H, A 
			LD	A, (PLOT_COLOUR)
			LD	(HL), A			; Write the colour out
			RET

; Draw Horizontal Line routine
; HL: Y coordinate
; BC: X pixel position 1
; DE: X pixel position 2
;
Draw_Horz_Line:		CALL	Adjust_SCRLPOS_P
;
			LD	B, C 			; B: Low X1, E: Low X2
			LD	D, L 			; D: Low Y
;
			LD	A, B			; Check if E (X1) > B (X2)
			CP	E 
			JR	NC,0F
			LD 	B, E			; Swap B and D
			LD 	E, A		
0:			PIXELAD				; HL: Screen Address
			LD	D, B 			; D: X2
			LD 	A, E			; Calculate index into table for X1
			AND	7
			LD 	BC, Plot_Line_LHS	; Get pixel data table
			ADD	BC, A
			LD 	A, (BC)			; 
			LD	E, A			; E: X1 pixel mask		
; HL: Screen address
;  E: X1 pixel data
			LD	A, D			; Calculate index into table for X2
			AND	7
			LD 	BC, Plot_Line_RHS
			ADD	BC, A 
			LD	A, D 			; Now the byte address for D
			RRCA
			RRCA 
			RRCA
			AND	31 
			LD	D, A 
			LD	A, L
			AND	%11100000
			OR	D			
			LD	D, A			; D: Screen address (low byte) for X2
			LD	A, (BC)	
			LD	C, A			; C: X2 pixel mask
			LD	A, D 			; Get number of bytes to write
			SUB	L 
			JR	NZ, 1F 			; If not zero, we're writing more than 1 byte
			LD	A, E			
			XOR	C 			; Otherwise, combine the pixel masks
			CPL 
			OR	(HL)			; OR with screen contents
			LD	(HL), A
			JP	Plot_Attribute		; Write out the attribute byte
;
1:			LD	B, A			; Loop counter
			LD	A, (HL)			; Write out the pixel data
			OR	E			; OR first byte with screen contents
			LD	(HL), A			
			LD	D, B			; Backup of loop counter
			LD	E, L 			; Backup of low byte
			INC	L
			DEC	B 
			JR	Z, 3F
			LD	A, 255
2:			LD	(HL), A			; Write out the main block
			INC	L
			DJNZ	2B
3:			LD	A, (HL)			; Write out the last byte
			OR	C			; OR last byte with screen contents
			LD	(HL), A 		
;
			LD	L, E			; Restore backup of low byte
			LD	B, D			; And loop counter
			CALL	Plot_Attribute		; Write the first attribute byte
			INC	B
4:			LD	(HL), A 		; Write out subsequent bytes
			INC	L
			DJNZ	4B
			RET 
;
; End-point masks for the horizontal lines
;
Plot_Line_LHS:		DB %11111111,%01111111,%00111111,%00011111,%00001111,%00000111,%00000011,%00000001
Plot_Line_RHS:		DB %10000000,%11000000,%11100000,%11110000,%11111000,%11111100,%11111110,%11111111

; Calculate Speccy colour for COLOUR and GCOL
;  C: New Colour
;  A: Existing attribute value
; Returns:
;  A: New attribute value
;
Get_Attr_Value:		BIT	7, C			; Check bit 7 of the new colour
			JR	Z, 1F			; for numbers >=128
			AND	%11000111		; And out the PAPER colour
			LD	B, A			; Store in B
			LD	A, C 
			AND	%00000111		; Get colour
			RLA 				; Shift into position
			RLA 
			RLA 
			OR	B 			; Or with existing colour
			RET
1:			AND	%11111000		; And out the INK colour
			LD	B, A			; Store in B
			LD	A, C 
			AND	%00000111		; Get colour
			OR	B 			; Or with existing colour
			RET

; Set the graphics colour
; HL: Value to set
;  C: Colour to set
;  B: Mode
;
Set_Plot_Colour:	LD	HL, PLOT_COLOUR
			JR	Set_Attr

; Set the text colour
; HL: Value to set
;  C: Colour to set
;  B: Mode
;
Set_Text_Colour:	LD	HL, TEXT_FOREGROUND
			CALL	Set_Attr
			LD	A, (HL)
			LD	(TEXT_BACKGROUND), A 	; For CLS
			RET
;			
Set_Attr:		LD 	A, B
			CP	6
			RET	NC
			CALL	SWITCH_A
			DEFW	Set_Attr_All
			DEFW	Set_Attr_Ink
			DEFW	Set_Attr_Paper
			DEFW	NEXT_GRAPHICS.Set_Border
			DEFW	Set_Attr_Bright
			DEFW	Set_Attr_Flash
;
Set_Attr_Paper:		SET	7, C 
			JR	Set_Attr_All
;
Set_Attr_Ink:		RES	7, C
Set_Attr_All:		LD	A, (HL)
			CALL	Get_Attr_Value
			LD	(HL), A
			RET
;
Set_Attr_Bright:	LD	A, C 
			AND	1 
			RRCA 
			RRCA 
			LD	B, A 
			LD	A, (HL)
			AND	%10111111
			OR	B 
			LD	(HL), A 
			RET
;
Set_Attr_Flash:		LD	A, C 
			AND	1 
			RRCA 
			LD	B, A 
			LD	A, (HL)
			AND	%01111111
			OR	B 
			LD	(HL), A 
			RET

			ENDMODULE