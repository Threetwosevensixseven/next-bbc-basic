;
; Title:	BBC Basic Interpreter - Z80 version
;		Spectrum Next Cursor Routines
; Author:	Dean Belfield
; Created:	28/05/2021
; Last Updated:	13/06/2021
;
; Modinfo:
; 07/06/2021:	Move and Display no longer do anything if the cursor is disabled
; 13/06/2021:	Cursor X now halved for Mode 3

			MODULE	NEXT_CURSOR

Sprite_Cursor:		EQU	127

; Initialise the cursor pattern
; E: Cursor width
;
Initialise:		Z80PORT	0x303B, Sprite_Cursor	; Select the sprite #
			LD	C, 8
1:			LD	B, E 
			LD	A, 255 
2:			Z80PRTA	0x5B
			DJNZ 	2B 
			LD	A, 8
			SUB 	E 
			LD	B, A 
			LD	A, 0x33			; Transparent colour
3:			Z80PRTA	0x5B
			DJNZ	3B
			DEC	C 
			JR	NZ, 1B
			LD	B, 64
4:			Z80PRTA	0x5B
			DJNZ	4B
			RET

; Calculate sprite offset
; A: Cursor X or Y position
;
Offset_X:		LD	L, A 
			LD	H, 0
			LD	A, (CHAR_COLS)
			CP	80
			JR	Z, 2F
			JR	1F
Offset_Y:		LD	L, A			; Multiply by 8
			LD	H, 0
1:			ADD	HL, HL
2:			ADD	HL, HL
			ADD	HL, HL
			LD	A, (CHAR_ROWS)		; Check for 256 column modes
			CP	32
			RET	Z 			; Ret if no border
			ADD	HL, 32			; Offset past border area
			RET

Move:			LD	A, (FLAGS)		; Don't do anything if the cursor is not visible
			BIT 	3, A 
			RET 	Z
			NEXTREG 0x34, Sprite_Cursor	; Set the sprite #
			LD	A, (CHAR_COLS)		; Check for 80 column mode
			CP	80
			LD	A, (CURSOR_X)		; Get cursor positions
			CALL	Offset_X
			LD	A, L
			LD	B, H
			NEXTREG	0x35, A			; LSB of X
			LD	A, (CURSOR_Y)
			CALL	Offset_Y
			LD	A, L
			NEXTREG	0x36, A 		; LSB of Y
			LD	A, B
			AND	1
			OR	%11110000
			NEXTREG	0x37, A			; 7-4: Pallete, 3,2: Mirror, 1: Rotate, 0: MSB of X
			LD	A, (FLAGS)
			SWAPNIB
			AND	%10000000
			LD	L, A 
			LD	A, (TIME)
			ADD	A, A 
			ADD	A, A
			AND 	L
			OR	%01111111
			NEXTREG	0x38, A 		; 7: Visible, 6: Extended, 5-0: Pattern
			LD	A, H			; MSB of Y
			AND 	1
			OR	%10000000	
			NEXTREG	0x39, A			; 7: 4-bit 6: Pattern, 4-1: Scaling, 0: MSB of Y
			RET

; Set cursor visibility
; 0: Off
; 1: On
;
Display:		AND	1
			ADD	A, A 
			ADD	A, A
			ADD	A, A 
			LD	B, A 
			LD	A, (FLAGS)
			AND	%11110111
			OR	B
			LD	(FLAGS), A
			BIT	3, A
			RET	NZ
			NEXTREG	0x34, Sprite_Cursor
			NEXTREG	0x38, 0
			RET

			ENDMODULE